.text
    TEST [run_third], [run_third]
    JNZ three_done
    TEST [run_second], [run_second]
    JNZ two_done
    TEST [run_first], [run_first]
    JNZ one_done

    MOV A, 4
    HWI 0xD
    MOV A, 0
    HWI 0xD

    MOV SP, 0x0


    MOV A, 0x1234
    MOV B, 0xABCD
    MOV C, 0xDEAD
    MOV D, 0xBEEF
    MOV X, 0xBABE
    MOV Y, 0xEDAB
    MOV BP, 0xDEAF
    CALL _RegisterDump
    MOV [run_first], 1

    BRK

one_done:
    PUSH 1
    PUSH 2
    PUSH 0xBEEF

    CALL _StackDump

    POP A
    POP A
    POP A

    MOV [run_second], 1
    BRK

two_done:
    CALL _MemoryDump
    MOV [run_third], 1
    BRK

three_done:
    brk

run_first: DW 0
run_second: DW 0
run_third: DW 0

;;;;;;;; dump code starts

; Dump all registers
_RegisterDump:
    PUSH A          ; [sp+1]
    PUSH SP ; +8    ; [sp]
    ADD [SP], 3   ; Skip the A push and the RET address
    PUSH BP ; +7
    PUSH Y  ; +6
    PUSH X  ; +5
    PUSH D  ; +4
    PUSH C  ; +3
    PUSH B  ; +2
    PUSH A  ; +1
    PUSH ____register_dump_text
    CALL _Printf
    POP A
    RET
____register_dump_text: DW "REGISTER DUMP\nA=%x\nB=%x\nC=%x\nD=%x\nX=%x\nY=%x\nBP=%x\nSP=%x\n",0
;_RegisterDump end

; Dump contents of the stack
_StackDump:
    PUSH BP ; BP+0
    MOV BP, SP
    ADD BP, 2   ; ignore BP+0 and RET address
    PUSH A

    PUSH ____stack_dump_text
    CALL _Printf

___stack_dump_loop:
    PUSH [BP]
    PUSH BP
    PUSH ____stack_dump_line
    CALL _Printf
    INC BP
    TEST BP, BP
    JNZ ___stack_dump_loop

    POP A
    POP BP
    RET
____stack_dump_text: DW "STACK DUMP\nADDR\tDATA\n",0
____stack_dump_line: DW "%x:\t%x\n",0
;_StackDump end

; Write all of RAM onto floppy
_MemoryDump:
    PUSH A
    PUSH X
    PUSH Y

    MOV X, 0
    MOV Y, 0
    MOV A, 3
___MemoryDump_loop:
    HWI 0xb
    ADD X, 1
    ADD Y, 512
    CMP X, 128
    JNZ ___MemoryDump_loop

    PUSH ____MemoryDump_text
    CALL _Printf

    POP Y
    POP X
    POP A
    RET
____MemoryDump_text: DW "MEMORY DUMP DONE\n",0
;_MemoryDump end

;;;;;;;; dump code ends

;;;;;;;; printf code starts

; A = printf(char* fmtstring, arg...)
; usage:
;
;   PUSH 0x15           ; hex value '%x'
;   PUSH [number]       ; unsigned integer '%u'
;   PUSH C              ; signed integer '%d'
;   PUSH str            ; null terminated string '%s'
;   PUSH fmt_str        ; Format string (in this example) "%s %d %u %x\n"
;   CALL _Printf        ; Will simply print out the arguments separated by spaces
;                       ; Ending with a newline
;
;
; NOTES:
;   -To print a '%' sign you need to use two, "%%"
;   -This function does not adhere to the limit of 40 message on the COM PORT
;   per game tick. Anything after 40*8 characters will not get printed unless
;   this is fixed by the user. I have noted the recommended spot to adjust in
;   the __Printf_print function
;
;
_Printf:
    PUSH BP ; [BP+0]
    MOV BP, SP
    PUSH B  ; -1
    PUSH C  ; -2
    PUSH D  ; -3
    PUSH X  ; -4
    PUSH Y  ; -5

    MOV Y, [BP+2]           ; Y is addr

    MOV X, ____printf_buffer    ; X is where we will need to print
    MOV B, 0                ; B will count from 8 to determine when we need to print
    MOV C, 0
___Printf_scan_loop:
    CALL __Printf_print

    TEST [Y], [Y]       ; [Y]&[Y] = 0 only if [Y]=0
    JZ ___Printf_loop_done ; End the print

    CMP [Y], 0x25       ; 0x25 = '%'
    JZ ___Printf_scan_found

    MOV [X], [Y]        ; Add any other character
    ADD Y, 1            ; Move to next character in fmt string
    ADD B, 1
    ADD X, 1            ; Move to next character in buffer
    JMP ___Printf_scan_loop

___Printf_scan_found:
    ADD Y,2
    TEST [Y-1], [Y-1]       ; if the % is followed by a null, just end
    JZ ___Printf_loop_done

    CMP [Y-1], 0x25     ; if it is another % escape it
    JZ ___Printf_found_percent

    ADD C, 1    ; increase which we want to get from the stack
    MOV D, 2
    ADD D, C
    ADD D, BP   ; get [BP+2+C]
    MOV D, [D]  ; D is now The next argument

    CMP [Y-1], 0x75   ; 'u'
    JZ ___Printf_found_u
    CMP [Y-1], 0x64   ; 'd'
    JZ ___Printf_found_d
    CMP [Y-1], 0x73   ; 's'
    JZ ___Printf_found_s
    CMP [Y-1], 0x78   ; 'x'
    JZ ___Printf_found_x

    ; If we found nothing skip the '%a' entirely
    JMP ___Printf_scan_loop

___Printf_found_percent:
    MOV [X], 0x25    ; Add a '%'
    ADD B, 1
    ADD X, 1
    JMP ___Printf_scan_loop
___Printf_found_u:
    PUSH ____itoa_result
    PUSH D
    CALL _UtoString
    MOV D, ____itoa_result
    JMP ___Printf_insert_loop
___Printf_found_d:
    PUSH ____itoa_result
    PUSH D
    CALL _DtoString
    MOV D, ____itoa_result
    JMP ___Printf_insert_loop    
___Printf_found_x:
    PUSH ____itoa_result
    PUSH D
    CALL _XtoString
    MOV D, ____itoa_result
    JMP ___Printf_insert_loop
___Printf_found_s:      ; S is already in a string
                        ; so we can just print it

___Printf_insert_loop:
    CALL __Printf_print

    TEST [D],[D]
    JZ ___Printf_scan_loop     ; when this new string ends go back to regular loop

    MOV [X], [D]

    ADD D, 1                ; Keep moving
    ADD B, 1                ; Keep tracking to the end of buffer
    ADD X, 1                ; Keep moving in the buffer
    JMP ___Printf_insert_loop

___Printf_loop_done:
    CMP B, 8                ; If the buffer is empty
    JZ ___Printf_stack_fix     ; we can skip this step

___Printf_zero_loop:           ; The buffer needs to 
    CMP B, 8                ; be padded to eight
    JZ ___Printf_zero_loop_done; characters with nulls
    MOV [X], 0
    ADD X, 1
    ADD B, 1
    JMP ___Printf_zero_loop

___Printf_zero_loop_done:
    CALL __Printf_print

___Printf_stack_fix:
    ; Move the return address from [BP+1] to [BP+1+args] (args include fmt string)
    ADD C, 1
    MOV A, C        ; number of things we need to ignore on the stack
    MOV B, BP
    ADD B, 1
    ADD B, C
    MOV [B], [BP+1]     ; Move return address [BP+1] to [BP+1+args]

___Printf_done:
    POP Y       ; [BP-5]
    POP X       ; [BP-4]
    POP D       ; [BP-3]
    POP C       ; [BP-2]
    POP B       ; [BP-1]
    POP BP      ; [BP-0]
    ; Move SP up the stack A times
    ADD SP, A
    RET
____printf_buffer: DW 8 DUP(0)
____itoa_result: DW 8 DUP(0)
;_Printf end

; If you need to stop hitting the 40 message limit
; by waiting for the next tick to keep printing
; do it here. But all the registers need to be
; kept intact.
__Printf_print:
    CMP B, 8
    JNZ ___Printf_print_done
    MOV X, ____printf_buffer
    MOV A, 3
    HWI 0x000D
    MOV B, 0
___Printf_print_done:
    RET
;__Printf_print end

;   Convert an unsigned decimal number into a null terminated string
;   
;   UtoString(char *addr, uint number)
;
;   PUSH result_addr
;   PUSH 5
;   CALL _UtoString
;
;   result_addr must be a buffer large enough to fit the given number, 8 is always enough
_UtoString:    
    PUSH BP ; bp+0
    MOV BP, SP
    PUSH A
    PUSH Y

    MOV A, [BP+2]       ; number to be converted
    PUSH 0

___UtoString_loop:
    MOV Y, 0
    DIV 10
    ADD Y, 0x30
    PUSH Y
    TEST A,A
    JNZ ___UtoString_loop

    MOV A, [BP+3]       ; address to store it
___UtoString_store_result_loop:
    POP Y
    MOV [A], Y
    ADD A, 1
    TEST Y,Y
    JNZ ___UtoString_store_result_loop

    POP Y
    POP A
    POP BP
    RET 2
;_UtoString end

;   Convert a signed decimal number into a null terminated string
;   
;   DtoString(char *addr, int number)
;
;   PUSH result_addr
;   PUSH -5
;   CALL _DtoString
;
;   result will be prefixed with '-' when negative
;
;   result_addr must be a buffer large enough to fit the given number, 8 is always enough
_DtoString:
    PUSH BP
    MOV BP, SP
    PUSH A
    PUSH X

    MOV X, [BP+3]   ; holder string
    MOV A, [BP+2]   ; number to be converted

    ; if neg add '-'
    ; then NEG the argument
    TEST A, A
    JNS ___DtoString_done
    NEG A               ; make number positive
    MOV [X], 0x2d       ; Add in the '0'
    ADD X, 1            ; increase string addr
___DtoString_done:
    PUSH X              ; push the string addr
    PUSH A              ; push the number to be converted
    CALL _UtoString

    POP X
    POP A
    POP BP
    RET 2
;_DtoString end

;   Convert a given number into a null terminated string
;
;   XtoString(char *addr, int number)
;
;   PUSH result_addr
;   PUSH 0xFFF
;   CALL _XtoString
;
;   The result is prefixed with '0x'
;
;   result_addr must be a buffer large enough to fit the given number, 8 is always enough
_XtoString:
    PUSH BP ; bp+0
    MOV BP, SP
    PUSH A
    PUSH X

    MOV X, [BP+2]       ; number to be converted
    PUSH 0

___XtoString_loop:
    MOV A, X
    AND A, 0x000F
    SHR X, 4
    CALL __XtoA
    PUSH A
    TEST X, X
    JNZ ___XtoString_loop

    MOV A, [BP+3]       ; address to store it

    MOV [A], 0x30       ; prefix with
    MOV [A+1], 0x78       ; '0x'
    ADD A, 2

___XtoString_store_result_loop:
    POP X
    MOV [A], X
    ADD A, 1
    TEST X,X
    JNZ ___XtoString_store_result_loop

    POP X
    POP A
    POP BP
    RET 2
;_XtoString end

; Return Hex value of the right-most 4 bits as
; a character code to be printed
; Super lazy
__XtoA:
    AND A, 0xF  ; Get only the last number
    ADD A, 0x30            ; If 0-9 add 0x30
    CMP A, 0x3A
    JL ___XtoA_done
    ADD A, 0x7                        ; if 10-15 add 0x40
___XtoA_done:
    RET
;_XtoA end

;;;;;;;; printf code ends